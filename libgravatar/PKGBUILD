# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libgravatar
pkgver=21.12.2
pkgrel=1
pkgdesc='KDE PIM library providing Gravatar support'
arch=(loongarch64 x86_64)
url='https://kontact.kde.org'
license=(LGPL)
depends=(pimcommon)
makedepends=(extra-cmake-modules boost qt5-tools doxygen)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('60ef0f2e74121fbb63b951a9f7059910e4afc2bffdc6589842794f3b19d84376'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
