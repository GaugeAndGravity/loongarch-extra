# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=polari
pkgver=41.0+r11+g0f0a4b54
pkgrel=1
pkgdesc="An IRC Client for GNOME"
url="https://wiki.gnome.org/Apps/Polari"
arch=(loongarch64 x86_64)
license=(GPL)
depends=(gjs gtk4 telepathy-glib telepathy-idle telepathy-mission-control
         telepathy-logger gspell libsoup3)
makedepends=(gobject-introspection yelp-tools appstream-glib git meson)
groups=(gnome-extra)
_commit=0f0a4b54142d8b424969f0b6ace6fc8b53b1d05d  # main
source=("git+https://gitlab.gnome.org/GNOME/polari.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd polari
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd polari
}

build() {
  arch-meson polari build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
