# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kjsembed
pkgver=5.91.0
pkgrel=1
pkgdesc='Embedded JS'
arch=(loongarch64 x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(qt5-svg ki18n kjs)
makedepends=(extra-cmake-modules qt5-tools kdoctools)
groups=(kf5-aids)
source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/portingAids/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('c0d07cba5fc4173279335f8bf696eb6e013696a19eadd3851f4f428942399e88'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
